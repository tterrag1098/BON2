plugins {
  id 'com.github.ben-manes.versions' version '0.25.0'
  id 'java'
  id 'maven'
  id 'maven-publish'
  id 'eclipse'
  id 'idea'
  id 'com.github.johnrengelman.shadow' version '2.0.4'
}

version = '2.4.0.' + (System.getenv('BUILD_NUMBER') ?: 'CUSTOM')
group = 'com.github.parker8283'
archivesBaseName = 'BON'

repositories {
    mavenCentral()
}

dependencies {
	compile 'com.google.code.gson:gson:2.8.5'
    compile 'net.sf.jopt-simple:jopt-simple:5.0.4'
	compile 'org.ow2.asm:asm:7.2'
    compile 'org.ow2.asm:asm-tree:7.2'
}

task copyAndReplace(type: Copy) {
    from(sourceSets.main.java) {
        include '**/BON2.java'
        expand 'DEV': project.version
    }
    from(sourceSets.main.java) {
        exclude '**/BON2.java'
    }
    into project.getBuildDir().toString() + "/sources/java"
}
tasks.compileJava.dependsOn copyAndReplace
tasks.compileJava.setSource(project.getBuildDir().toString() + '/sources/java')

def MANIFEST = manifest {
    attributes([
        'Main-Class': 'com.github.parker8283.bon2.BON2',
        'Implementation-Title': 'Bearded Octo Nemesis 2',
        'Implementation-Version': project.version,
        'Built-With': 'Gradle v' + project.getGradle().getGradleVersion()
    ] as LinkedHashMap)
}

jar {
    manifest.from(MANIFEST)
}

shadowJar {
    classifier 'all'
    manifest.from(MANIFEST)
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
    manifest.from(MANIFEST)
}

artifacts {
    archives shadowJar
    archives jar
    archives sourcesJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact sourcesJar
            artifact shadowJar
            artifact jar
            pom {
                name = project.archivesBaseName
                packaging = 'jar'
                description = 'A tool that remaps binary jar files'
                url = 'https://github.com/tterrag1098/BON2'

                scm {
                    url = 'https://github.com/tterrag1098/BON2'
                    connection = 'scm:git:git://github.com/tterrag1098/BON2.git'
                    developerConnection = 'scm:git:git@github.com:tterrag1098/BON2.git'
                }

                issueManagement {
                    system = 'github'
                    url = 'https://github.com/tterrag1098/BON2/issues'
                }

                developers {
                    developer {
                        id = 'cpw'
                        name = 'cpw'
                    }
                    developer {
                        id = 'LexManos'
                        name = 'Lex Manos'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            if (project.hasProperty('mavenUrl')) {
                credentials {
                    username project.properties.mavenUser
                    password project.properties.mavenPassword
                }
                url project.properties.mavenUrl
            } else {
                url 'file://' + rootProject.file('repo').getAbsolutePath()
            }
        }
    }
}

wrapper {
    gradleVersion = '5.6.2'
}

dependencyUpdates {
  rejectVersionIf {
    isNonStable(it.candidate.version)
  }
  rejectVersionIf {
    isNonStable(it.candidate.version) && !isNonStable(it.currentVersion)
  }
}
